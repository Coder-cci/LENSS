@ECHO OFFà
REM - Updated AUTOEXEC file, for 386 or better hardware

REM - set basic environment

set DOSDRV=C:
set DOSDIR=C:\FDOS\FreeDOS

path %DOSDIR%\BIN
if not exist %DOSDIR%\LINKS\NUL goto NOLINKS
path %PATH%;%DOSDIR%\LINKS
:NOLINKS

set NLSPATH=%DOSDIR%\NLS
set HELPPATH=%DOSDIR%\HELP

set TEMP=%DOSDIR%\TEMP
set TMP=%TEMP%

set LANG=EN
set TZ=UTC

REM Emergency Mode, no drivers should be loaded
if "%CONFIG%"=="5" goto END

REM - Internationalization settings

REM nlsfunc %DOSDIR%\BIN\COUNTRY.SYS
REM display CON=(EGA,858,2)
REM mode CON CP PREP=((858) %DOSDIR%\CPI\EGA.CPX)
REM keyb US,858,%DOSDIR%\BIN\KEYBOARD.SYS
REM chcp 858
REM mkeyb UK

rem codepage settings 
REM NLSFUNC %dosdir%\BIN\COUNTRY.SYS
REM DISPLAY CON=(EGA,858,2)
REM MODE CON CP PREP=((858) %dosdir%\CPI\EGA.CPX)
REM KEYB US,858,%dosdir%\bin\keyboard.sys
REM CHCP 858
REM MKEYB UK
rem keyboard settings 


set OS_NAME=FreeDOS
set OS_VERSION=1.4

REM - cfgfile and autofile might be deprecated in future
REM - At boot, FreeDOS can use CONFIG.SYS, but it prefers FDCONFIG.SYS

set CFGFILE=%DOSDRV%\CONFIG.SYS
if exist %DOSDRV%\FDCONFIG.SYS set CFGFILE=%DOSDRV%\FDCONFIG.SYS
set AUTOFILE=%0
alias cfg=edit %CFGFILE%
alias auto=edit %AUTOFILE%

if "%CONFIG%"=="4" goto DOSLOW
goto DOSHIGH

:DOSLOW
ctmouse
goto CONFIGS

:DOSHIGH

lh fdapm APMDOS
lh ctmouse
REM lh share

if not exist %DOSDIR%\BIN\DOSLFN.COM goto NOLFN
lh %DOSDIR%\BIN\DOSLFN.COM
REM - Add other stuff here once LFN is loaded..
:NOLFN

if not exist %DOSDIR%\BIN\CDROM.BAT goto NOCDROM
call %DOSDIR%\BIN\CDROM.BAT
REM - Add other stuff here once CDROM is loaded..
:NOCDROM

if not exist %DOSDIR%\BIN\FDNET.BAT goto NONET
call %DOSDIR%\BIN\FDNET.BAT
REM - if errorlevel 1 goto NONET
REM - Add other stuff here once FDNET is loaded..
:NONET

:CONFIGS
REM - load other configs using external BAT files

if not exist %DOSDIR%\BIN\FDASSIST.BAT goto NOASSIST
call %DOSDIR%\BIN\FDASSIST.BAT
:NOASSIST

REM - additional customization and local settings

set BLASTER=A220 I5 D1 H5 P330
set DIRCMD=/O:GNE /Y
set COPYCMD=/-Y

alias reset=fdisk /reboot
alias reboot=fdapm warmboot
alias halt=fdapm poweroff
alias shutdown=fdapm poweroff

MEM /C /N

REM - Display drive for CD-ROM
if not exist %DOSDIR%\BIN\CDROM.BAT goto NOCDSTATUS
echo.
call %DOSDIR%\BIN\CDROM.BAT status
:NOCDSTATUS

REM - Display "Welcome to FreeDOS"
if not exist %DOSDIR%\BIN\WELCOME.BAT goto NOWELCOME
call %DOSDIR%\BIN\WELCOME.BAT
:NOWELCOME

:END
